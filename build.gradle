buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven { 
			name = 'Jitpack'
			url 'https://jitpack.io/'
		}
	}
	dependencies {
		classpath 'com.github.Chocohead:Fabric-Loom:d823377'
	}
}

plugins {
	id 'checkstyle'
}

def getSubprojectVersion(project, ver) {
	return "${project.mod_version}-$ver"
}

def moduleDependencies(Project project, String... projectNames) {
	project.with {
		def modules = projectNames.collect { dependencies.project(path: ":$it", configuration: 'dev') }

		dependencies {
			modules.each {
				implementationOnly it
			}
		}
		publishing {
			publications {
				mavenJava(MavenPublication) {
					pom.withXml {
						addDependency(it, 'compile', *modules)
					}
				}
			}
		}
	}
}

void addDependency(XmlProvider xml, String scope, Object... dependencies) {
	def dependenciesNode = net.fabricmc.loom.util.GroovyXmlUtil.getOrCreateNode(xml.asNode(), 'dependencies')

	dependencies.each {dependency ->
		dependenciesNode.appendNode('dependency').with {
			appendNode('groupId', dependency.group)
			appendNode('artifactId', dependency.name)
			appendNode('version', dependency.version)
			appendNode('scope', scope)
		}
	}
}


allprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	//Group is common for all API modules
	group = project.maven_group

	repositories {
		maven { 
			name = 'Jitpack'
			url 'https://jitpack.io/'
		}
	}

	configurations {
		implementationOnly //A non-transitive implementation
		runtimeClasspath.extendsFrom implementationOnly
		compileClasspath.extendsFrom implementationOnly
		
	}

	minecraft {
		addLibraryFilter {library ->
			return !(library.startsWith('net.minecraft:launchwrapper:') ||
						library.startsWith('net.sf.jopt-simple:jopt-simple:') ||
						library.startsWith('org.ow2.asm:asm-all:'))
		}
	}

	dependencies {
		//to change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"

		mappings loom.fromCommit('minecraft-cursed-legacy/Plasma', '59b928b') {spec ->
			spec.version = 'b1.7.3-15'
		}

		modImplementation("com.github.minecraft-cursed-legacy:cursed-fabric-loader:${project.loader_version}") {
			transitive false //Avoid leaking Loader's dependencies forwards
		}
	}

	sourceSets {
		test {
			compileClasspath += main.compileClasspath + main.output
			runtimeClasspath += main.runtimeClasspath + main.output
		}
	}

	afterEvaluate {
		processResources {
			inputs.property 'version', project.version

			from(sourceSets.main.resources.srcDirs) {
				include 'fabric.mod.json'
				expand 'version': project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude 'fabric.mod.json'
			}
		}
	}

	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				afterEvaluate {
					artifact(remapJar) {
						builtBy remapJar
					}

					artifact(sourcesJar) {
						builtBy remapSourcesJar
					}
				}
			}
		}
	}
}

import net.fabricmc.loom.task.RunClientTask
import net.fabricmc.loom.task.RunServerTask

subprojects {subproject ->
	task runTestmodClient(type: RunClientTask) {
		classpath sourceSets.test.runtimeClasspath
	}

	task runTestmodServer(type: RunServerTask) {
		classpath sourceSets.test.runtimeClasspath
	}

	assert this.remapJar != remapJar //No accidents moving this around
	this.remapJar.dependsOn(remapJar)

	configurations {
		out {
			canBeConsumed = true
			canBeResolved = false
		}
		dev {
			canBeConsumed = true
			canBeResolved = false
		}
		test {
			canBeConsumed = true
			canBeResolved = false
		}
	}

	task testJar(type: Jar) {
		from sourceSets.test.output
		classifier 'test'
	}

	artifacts {
		out remapJar
		dev jar
		test testJar
	}

	//Attach the subproject to the root project
	this.dependencies {
		implementationOnly project(path: ":$name", configuration: 'dev')
		testImplementation project(path: ":$name", configuration: 'test')
		afterEvaluate {//Do this after Loom tries to work out how included projects relate to remapJar tasks
			include project(path: ":$name", configuration: 'out')
		}
	}

	//Mark the subproject as a compile time dependency of the root project
	this.publishing {
		publications {
			mavenJava(MavenPublication) {
				pom.withXml {xml ->
					addDependency(xml, 'compile', subproject)
				}
			}
		}
	}
}

//Subprojects will set these themselves
archivesBaseName = archives_base_name
version = mod_version

dependencies {
	// CoreLib for Models, only included as part of the combined distribution
	include modApi('com.github.paulevsGitch:B.1.7.3-CoreLib:v.1.3.0') {
		exclude(group: 'tk.valoeghese') //ZoesteriaConfig has disppeared from here
	}
}
